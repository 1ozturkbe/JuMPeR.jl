{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "![](http://iainnz.github.io/JuMPeR.jl/logo.svg)\n",
    "\n",
    "# JuMPeR Tutorial\n",
    "### MIT Robust Optimization Class, Feb 20th, 2015\n",
    "\n",
    "Today I'm going to be teaching you about JuMPeR, an extension for JuMP for **robust optimization**. I'm somewhat assuming you are familar with Julia/JuMP, but we'll review JuMP in the process of learning JuMPeR.\n",
    "\n",
    "## Motivational Problem: Portfolio Optimization\n",
    "\n",
    "We're going to be looking at a fairly simple problem today, **portfolio optimization**. The general goal is to select a *portfolio* of assets to maximize return, but we also take into account the \"risk\" of the portfolio. In general, the higher the return, the higher the risk. Our goal is to find portfolios that trade off return and risk in such a way as to make sure that, for a given risk appetite/tolerance, we find the highest return."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Review of JuMP: Deterministic Portfolio Optimization\n",
    "\n",
    "To get warmed up, we'll model the classic Markowitz portfolio optmization problem. Given $n$ assets, with average returns $\\mathbf{r}$ and covariance matrix of returns $\\mathbf{\\Sigma}$, we can express the problem of finding the minimum risk portfolio for a given return as\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "\\min_{\\mathbf{x}\\geq 0} \\quad & \\mathbf{x}^T \\mathbf{\\Sigma} \\mathbf{x} \\\\\n",
    "\\text{subject to}       \\quad & \\mathbf{e}^T \\cdot \\mathbf{x} = 1 \\\\\n",
    "                              & \\mathbf{r}^T \\cdot \\mathbf{x} \\geq R\n",
    "\\end{alignat}\n",
    "$$\n",
    "You can imagine a flipped problem where we maximize return subject to a risk tolerance. We also note that we've defined risk to be the variance of the resulting portfolio - this isn't probably a good idea in practice, but this is an older-style model."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# We'll define some helper functions to generate data\n",
    "# and to simulate the performance of our portfolios.\n",
    "# Make sure you have Distributions installed first\n",
    "#Pkg.add(\"Distributions\")\n",
    "using Distributions\n",
    "function generate_data(n, num_obs)\n",
    "    data = zeros(num_obs, n)\n",
    "\n",
    "    # Linking factors\n",
    "    beta = [(i-1.0)/n for i = 1:n] \n",
    "\n",
    "    for obs_ind = 1:num_obs\n",
    "        # Common market factor, mean 3%, sd 5%, truncate at +- 3 sd\n",
    "        z = rand(Normal(0.03, 0.05))\n",
    "        z = max(z, 0.03 - 3*0.05)\n",
    "        z = min(z, 0.03 + 3*0.05)\n",
    "\n",
    "        for asset_ind = 1:n\n",
    "            # Idiosyncratic contribution, mean 0%, sd 5%, truncated at +- 3 sd\n",
    "            asset = rand(Normal(0.00, 0.05))\n",
    "            asset = max(asset, 0.00 - 3*0.05)\n",
    "            asset = min(asset, 0.00 + 3*0.05)\n",
    "            data[obs_ind, asset_ind] = beta[asset_ind] * z + asset\n",
    "        end\n",
    "    end\n",
    "    return 100*data\n",
    "end\n",
    "\n",
    "# Alternative data generation function\n",
    "#function generate_data(n, num_obs)\n",
    "#    # Single factor CAPM model\n",
    "#    z = .1 * randn(num_obs) + .2\n",
    "#    betas = linspace(0, 1, n)\n",
    "#    z * betas' + .05*randn(num_obs, n)\n",
    "#end\n",
    "\n",
    "srand(2000)\n",
    "returns = generate_data(5, 10000)\n",
    "println(\"Mean returns:\")\n",
    "println( round( mean(returns,1), 2) )\n",
    "println(\"Covariance of returns:\")\n",
    "println( round( cov(returns),    3) )\n",
    "\n",
    "function eval_portfolio(returns, x)\n",
    "    port_returns = returns * x\n",
    "    # Collect summary stats for the returns\n",
    "    sort!(port_returns)\n",
    "    m = length(port_returns)\n",
    "    @printf(\" Minimum: %6.3f\\n\", port_returns[1])\n",
    "    @printf(\"25th per: %6.3f\\n\", port_returns[div(m,4)])\n",
    "    @printf(\"50th per: %6.3f\\n\", port_returns[div(m,2)])\n",
    "    @printf(\"75th per: %6.3f\\n\", port_returns[div(3m,4)])\n",
    "    @printf(\" Maximum: %6.3f\\n\", port_returns[end])\n",
    "    @printf(\"    Mean: %6.3f\\n\", mean(port_returns))\n",
    "    @printf(\"Std. Dev: %6.3f\\n\", std(port_returns))\n",
    "    @printf(\"Variance: %7.4f\\n\", var(port_returns))\n",
    "end\n",
    "\n",
    "println()\n",
    "println(\"Stats for a portfolio that invests in all equally\")\n",
    "eval_portfolio(returns, [0.2, 0.2, 0.2, 0.2, 0.2])\n",
    "println()\n",
    "println(\"Maximum return portfolio\")\n",
    "eval_portfolio(returns, [0.0, 0.0, 0.0, 0.0, 1.0])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# We can finally look at the deterministic optimization problem\n",
    "using JuMP, Gurobi\n",
    "\n",
    "function solve_det_port(returns, min_return)\n",
    "    n = size(returns,2)\n",
    "    # Create a JuMP Model, using Gurobi as the solver\n",
    "    detport = Model(solver=GurobiSolver())\n",
    "    @defVar(detport, 0 <= x[1:n] <= 1)\n",
    "    # Must be a valid allocation\n",
    "    @addConstraint(detport, sum{x[i], i=1:n} == 1)\n",
    "    # Must make min return\n",
    "    r = mean(returns,1)\n",
    "    @addConstraint(detport, sum{r[i]*x[i], i=1:n} >= min_return)\n",
    "    # Objective: minimize risk (variance)\n",
    "    Σ = cov(returns)\n",
    "    @setObjective(detport, Min, sum{Σ[i,j]*x[i]*x[j], i=1:n, j=1:n})\n",
    "    # Solve it!\n",
    "    solve(detport)\n",
    "    # Return the portfolio\n",
    "    getValue(x)[:]\n",
    "end\n",
    "\n",
    "# Try it out\n",
    "srand(1988)\n",
    "train_returns = generate_data(10, 10000)\n",
    "# Must make at least...\n",
    "detsol = solve_det_port(train_returns, 1.216)\n",
    "println(\"Portfolio\")\n",
    "println(round(detsol,2))\n",
    "println(\"Portfolio on training data\")\n",
    "eval_portfolio(train_returns, detsol)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Learning JuMPeR: Robust Portfolio Optimization\n",
    "\n",
    "In a robust portfolio optmization problem, we still have our $n$ assets and our goal is to balance risk and return. Instead of using the mean and variance of the portfolio, we'll instead try to maximize the worst-case return over our uncertainty set, i.e.\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "\\min_{\\mathbf{x}\\geq 0} \\quad & \\min_{r \\in U} \\mathbf{r}^T \\cdot \\mathbf{x} \\\\\n",
    "\\text{subject to}       \\quad & \\mathbf{e}^T \\cdot \\mathbf{x} = 1\n",
    "\\end{alignat}\n",
    "$$\n",
    "The tradeoff between risk and return is baked into the uncertainty set $U$.\n",
    "\n",
    "### Uncertainty Set 1: Box\n",
    "\n",
    "We'll start with a somewhat silly uncertainty set, a box. Given past returns, calculate average return $\\bar{\\mathbf{r}}$ and the standard deviation of return $\\bar{\\mathbf{\\sigma}}$ for each asset. Then our uncertainty set is\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "U & = \\left\\{ \\bar{r}_i - \\alpha*\\sigma_i \\leq r \\leq \\bar{r}_i + \\alpha*\\sigma_i \\right\\}\n",
    "\\end{alignat}\n",
    "$$\n",
    "where $\\alpha$ controls the size of the box - $\\alpha=0$ is just the average return."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# Need to use JuMP as well as JuMPeR\n",
    "using JuMP, JuMPeR\n",
    "\n",
    "function solve_box_port(returns, α)\n",
    "    n = size(returns,2)\n",
    "    # Create a JuMPeR RobustModel - mostly like a JuMP Model\n",
    "    # in terms of construction\n",
    "    boxport = RobustModel(solver=GurobiSolver())\n",
    "    # Create our variables\n",
    "    @defVar(boxport, 0 <= x[1:n] <= 1)\n",
    "    # Must be a valid allocation\n",
    "    @addConstraint(boxport, sum{x[i], i=1:n} == 1)\n",
    "\n",
    "    # Uncertainty\n",
    "    # First lets calculate the data we need\n",
    "    avg_r = mean(returns,1)\n",
    "    σ     = std(returns,1)\n",
    "    # Create our Uncertains\n",
    "    # This is pretty much the same as constructing a variable!\n",
    "    @defUnc(boxport, avg_r[i] - α*σ[i] <= r[i=1:n] <= avg_r[i] + α*σ[i])\n",
    "    # We don't need any additional constraints right now on our\n",
    "    # uncertainty set than those ranges.\n",
    "    \n",
    "    # Objective\n",
    "    # JuMPeR does not allow for uncertain objectives - they must\n",
    "    # be written as constraints.\n",
    "    @defVar(boxport, obj)\n",
    "    @setObjective(boxport, Max, obj)\n",
    "    # Uncertains can be mixed with variables just like data\n",
    "    @addConstraint(boxport, obj <= sum{r[i]*x[i], i=1:n})\n",
    "    \n",
    "    # Print it to have a look\n",
    "    print(boxport)\n",
    "    \n",
    "    # Solve it!\n",
    "    solve(boxport)\n",
    "    #solve(boxport, debug_printfinal=true)\n",
    "    \n",
    "    # Return the portfolio\n",
    "    getValue(x)[:]\n",
    "end\n",
    "\n",
    "# Try it out\n",
    "srand(1988)\n",
    "train_returns = generate_data(10, 10000)\n",
    "boxsol = solve_box_port(train_returns, 1)\n",
    "# Look at how we now have 12 rows, 31 columns\n",
    "println(\"Portfolio\")\n",
    "println(round(boxsol,2))\n",
    "println(\"Portfolio on training data\")\n",
    "eval_portfolio(train_returns, boxsol)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Uncertainty Set 2: Polyhedral\n",
    "\n",
    "Lets do one step better than a box: a polyhedron. Polyhedral uncertainty sets are pretty popular in the literature because they don't turn (MI)LPs into (MI)SOCPs, even though an ellipsoidal set would seem to be more appropriate. The set we will do is inspired by a definition of a multivariate normal distribution. Given average returns $\\bar{\\mathbf{r}}$, a covariance matrix of returns $\\Sigma$, we can say (assuming returns are normal)\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "\\mathbf{r} & = \\Sigma^\\frac{1}{2} \\mathbf{z} + \\bar{\\mathbf{r}}\n",
    "\\end{alignat}\n",
    "$$\n",
    "We'll use that to inspire the uncertainty set:\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "U & = \\left\\{ \\mathbf{r} = \\Sigma^{0.5} \\mathbf{z} + \\bar{\\mathbf{r}}, \\quad \\| \\mathbf{z} \\|_1 \\leq \\Gamma \\right\\}\n",
    "\\end{alignat}\n",
    "$$\n",
    "where $\\Gamma$ controls the conservativeness."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "function solve_poly_port(returns, Γ)\n",
    "    n = size(returns,2)\n",
    "\n",
    "    polyport = RobustModel(solver=GurobiSolver())\n",
    "    @defVar(polyport, 0 <= x[1:n] <= 1)\n",
    "    @addConstraint(polyport, sum{x[i], i=1:n} == 1)\n",
    "\n",
    "    # Uncertainty\n",
    "    # Calculate the data we need\n",
    "    avg_r = mean(returns,1)\n",
    "    Σ     = cov(returns)\n",
    "    Σhalf = chol(Σ)\n",
    "    \n",
    "    # Create our Uncertains\n",
    "    # The returns\n",
    "    @defUnc(polyport, r[i=1:n])\n",
    "    # The factors\n",
    "    @defUnc(polyport, z[i=1:n])\n",
    "    # The absolute value of the factors\n",
    "    @defUnc(polyport, zabs[i=1:n] >= 0)\n",
    "    # Link all these together\n",
    "    # Constraints that are JUST on Uncertains are added\n",
    "    # to the uncertainty set automatically, not to the problem\n",
    "    for i in 1:n\n",
    "        @addConstraint(polyport, r[i] == sum{Σhalf[i,j]*z[j], j=1:n} + avg_r[i])\n",
    "        @addConstraint(polyport, zabs[i] >=  z[i])\n",
    "        @addConstraint(polyport, zabs[i] >= -z[i])\n",
    "    end\n",
    "    @addConstraint(polyport, sum{zabs[i], i=1:n} <= Γ)\n",
    "    \n",
    "    # Objective - same as before\n",
    "    @defVar(polyport, obj <= 10)\n",
    "    @setObjective(polyport, Max, obj)\n",
    "    @addConstraint(polyport, obj <= sum{r[i]*x[i], i=1:n})\n",
    "    \n",
    "    # Print it to have a look\n",
    "    #print(polyport)\n",
    "    \n",
    "    # Solve it!\n",
    "    solve(polyport)\n",
    "    #solve(polyport, prefer_cuts=true)\n",
    "    #solve(boxport, debug_printfinal=true)\n",
    "    \n",
    "    # Return the portfolio\n",
    "    getValue(x)[:]\n",
    "end\n",
    "\n",
    "# Try it out\n",
    "srand(1988)\n",
    "train_returns = generate_data(10, 10000)\n",
    "polysol = solve_poly_port(train_returns, 2)\n",
    "println(\"Polyhedral Portfolio\")\n",
    "println(round(polysol,2))\n",
    "println(\"Polyhedral Portfolio on training data\")\n",
    "eval_portfolio(train_returns, polysol)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Uncertainty Set 3: Ellipsoidal\n",
    "\n",
    "We can do the same uncertainty set as before, with the norm changed to a 2-norm, i.e. the set is an ellipse\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "U & = \\left\\{ \\mathbf{r} = \\Sigma^{0.5} \\mathbf{z} + \\bar{\\mathbf{r}}, \\quad \\| \\mathbf{z} \\|_2 \\leq \\Gamma \\right\\}\n",
    "\\end{alignat}\n",
    "$$\n",
    "where $\\Gamma$ controls the conservativeness again."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "function solve_ell_port(returns, Γ)\n",
    "    n = size(returns,2)\n",
    "\n",
    "    ellport = RobustModel(solver=GurobiSolver())\n",
    "    @defVar(ellport, 0 <= x[1:n] <= 1)\n",
    "    @addConstraint(ellport, sum{x[i], i=1:n} == 1)\n",
    "\n",
    "    # Uncertainty\n",
    "    # Calculate the data we need\n",
    "    avg_r = mean(returns,1)\n",
    "    Σ     = cov(returns)\n",
    "    Σhalf = chol(Σ)\n",
    "    \n",
    "    # Create our Uncertains\n",
    "    # The returns\n",
    "    @defUnc(ellport, r[i=1:n])\n",
    "    # The factors\n",
    "    @defUnc(ellport, z[i=1:n])\n",
    "    # Link all these together\n",
    "    # Constraints that are JUST on Uncertains are added\n",
    "    # to the uncertainty set automatically, not to the problem\n",
    "    for i in 1:n\n",
    "        @addConstraint(ellport, r[i] == sum{Σhalf[i,j]*z[j], j=1:n} + avg_r[i])\n",
    "    end\n",
    "    # Add the norm/ellipse constraint\n",
    "    addEllipseConstraint(ellport, [z[i] for i in 1:n], Γ)\n",
    "    \n",
    "    # Objective - same as before\n",
    "    @defVar(ellport, obj)\n",
    "    @setObjective(ellport, Max, obj)\n",
    "    @addConstraint(ellport, obj <= sum{r[i]*x[i], i=1:n})\n",
    "    \n",
    "    # Print it to have a look\n",
    "    #print(ellport)\n",
    "    \n",
    "    # Solve it!\n",
    "    solve(ellport)\n",
    "    #solve(ellport, debug_printfinal=true)\n",
    "    #solve(ellport, prefer_cuts=true)\n",
    "    \n",
    "    # Return the portfolio\n",
    "    getValue(x)[:]\n",
    "end\n",
    "\n",
    "# Try it out\n",
    "srand(1988)\n",
    "train_returns = generate_data(10, 10000)\n",
    "ellsol = solve_ell_port(train_returns, 10)\n",
    "println(\"Ellipsoidal Portfolio\")\n",
    "println(round(ellsol,2))\n",
    "println(\"Ellipsoidal Portfolio on training data\")\n",
    "eval_portfolio(train_returns, ellsol)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Advanced JuMPeR: Oracles\n",
    "\n",
    "JuMPeR's design is focussed around *oracles*. Oracles are responsible for taking RO problems, or parts of them, and transforming them into something solveable. That could be reformulating uncertain constraints into deterministic constraints, it could be generating cutting-planes, or something else entirely. Oracles are also intimately connected to uncertainty sets - for example, we can provide data to an oracle from which it can generate cutting-planes - an uncertainty set  never needs to explictly constructed. Finally oracles are interchangeable - you can obtain oracles from others or create your own to allow others to explore the performance of different sets and implementations.\n",
    "\n",
    "JuMPeR currently comes with three oracles:\n",
    "\n",
    " * `GeneralOracle`, the default oracle. It takes an explicit polyhedral/ellipsoidal representation of the uncertainty set and can generate a reformulation or use cutting-planes.\n",
    " * `GeneralGraphOracle`, a variation on `GeneralOracle`. This oracle attempts to discover if the uncertain parameters actually belong to seperate, disjunct uncertainty sets. This allows it to generate smaller reformulations, and a seperator cut generator for each set if using cutting-planes.\n",
    " * `BertSimOracle`, implements the uncertainty set described in the 2004 paper *The Price of Robustness* by Bertsimas and Sim. Will generate cutting-planes efficiently using sorting instead of solving an LP. The `GeneralOracle` should be used if a reformulation is desired.\n",
    " \n",
    "Today we'll experiment with this feature by using some of the uncertainty sets defined in\n",
    "\n",
    "> [D. Bertsimas, V. Gupta and N. Kallus, \"Data-Driven Robust Optimization\"](http://arxiv.org/abs/1401.0212)\n",
    "\n",
    "that have been turned into JuMPeR oracles by Vishal Gupta, and make available as [DDUS.jl](https://github.com/vgupta1/DDUS.jl)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# First, install DDUS.jl\n",
    "#Pkg.clone(\"https://github.com/vgupta1/DDUS.jl.git\")\n",
    "import DDUS"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "function solve_dd_port(returns, α, β)\n",
    "    n = size(returns,2)\n",
    "\n",
    "    ddport = RobustModel(solver=GurobiSolver(OutputFlag=0))\n",
    "    @defVar(ddport, 0 <= x[1:n] <= 1)\n",
    "    @addConstraint(ddport, sum{x[i], i=1:n} == 1)\n",
    "    \n",
    "    # Create our Uncertains\n",
    "    @defUnc(ddport, r[i=1:n])\n",
    "    # The oracle defines an uncertainty set implicitly\n",
    "    # from data - we don't provide it\n",
    "    # There are many uncertainty sets defined in the paper\n",
    "    # We'll test out the UCS set\n",
    "    oracle = DDUS.UCSOracle(returns, α, β)\n",
    "    # We need to tell JuMPeR to use the UCSOracle for the\n",
    "    # uncertaint constraints\n",
    "    setDefaultOracle!(ddport, oracle)\n",
    "    \n",
    "    # Objective - same as before\n",
    "    @defVar(ddport, obj <= 1000)\n",
    "    @setObjective(ddport, Max, obj)\n",
    "    @addConstraint(ddport, obj <= sum{r[i]*x[i], i=1:n})\n",
    "\n",
    "    # Solve it!\n",
    "    solve(ddport, prefer_cuts=true)\n",
    "    #solve(ddport, prefer_cuts=true, debug_printfinal=true)\n",
    "    #solve(ellport, prefer_cuts=true)\n",
    "    \n",
    "    # Return the portfolio\n",
    "    getValue(x)[:]\n",
    "end\n",
    "\n",
    "# Try it out\n",
    "srand(1988)\n",
    "train_returns = generate_data(10, 1000)\n",
    "ddsol = solve_dd_port(train_returns, 0.1, 0.2)\n",
    "println(\"Data-driven (UCS) Portfolio\")\n",
    "println(round(ddsol,2))\n",
    "println(\"Data-driven (UCS) Portfolio on training data\")\n",
    "eval_portfolio(train_returns, ddsol)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.3.6",
   "language": "julia",
   "name": "julia 0.3"
  },
  "language_info": {
   "name": "julia",
   "version": "0.3.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
