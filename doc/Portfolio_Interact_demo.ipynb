{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "![](http://iainnz.github.io/JuMPeR.jl/logo.svg)\n",
    "\n",
    "# JuMPeR Demonstration\n",
    "\n",
    "## Portfolio Optimization\n",
    "\n",
    "This notebook is based off a tutorial given at MIT on Feb 20th, 2015. That notebook is also in this folder, and covers more content than this notebook, including ellipsoidal uncertainty sets and cutting-edge data-driven sets.\n",
    "\n",
    "Here we use [Interact.jl](https://github.com/JuliaLang/Interact.jl) to provide interactivity, and [Gadfly.jl](https://gadflyjl.org) for visualizations. These features will not work if you looking at a static copy of this notebook (via, e.g. [nbviewer.ipython.org](nbviewer.ipython.org) - you must be running the notebook.\n",
    "\n",
    "We're going to be looking at a fairly simple problem today, **portfolio optimization**. The general goal is to select a *portfolio* of assets to maximize return, but we also take into account the \"risk\" of the portfolio. In general, the higher the return, the higher the risk. Our goal is to find portfolios that trade off return and risk in such a way as to make sure that, for a given risk appetite/tolerance, we find the highest return."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# You will need some packages\n",
    "using Distributions  # For data generation\n",
    "using JuMP  # a modeling language for optimization\n",
    "using JuMPeR  # adds robust optimization support to JuMP\n",
    "using Interact  # add interactivity to IJulia\n",
    "using Gadfly  # visualization\n",
    "# You will also need a solver - any LP solver should\n",
    "# work. See juliaopt.org for a list of available solvers.\n",
    "# I'll be using Gurobi\n",
    "using Gurobi\n",
    "# Packages take quite a while to load in Julia 0.3\n",
    "# Gadfly, in particular, takes many seconds\n",
    "# Fortunately, this should be a one-time penalty!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# We'll define some helper functions to generate data\n",
    "# and to simulate the performance of our portfolios.\n",
    "# These returns are normally distributed, which isn't\n",
    "# very ideal/at all realistic, but it will do.\n",
    "function generate_data(n, num_obs)\n",
    "    data = zeros(num_obs, n)\n",
    "\n",
    "    # Linking factors\n",
    "    beta = [(i-1.0)/n for i = 1:n] \n",
    "\n",
    "    for obs_ind = 1:num_obs\n",
    "        # Common market factor, mean 3%, sd 2%, truncate at +- 3 sd\n",
    "        z = rand(Normal(0.03, 0.02))\n",
    "        z = max(z, 0.03 - 3*0.02)\n",
    "        z = min(z, 0.03 + 3*0.02)\n",
    "\n",
    "        for asset_ind = 1:n\n",
    "            # Idiosyncratic contribution, mean 0%, sd 3%, truncated at +- 3 sd\n",
    "            asset = rand(Normal(0.00, 0.03))\n",
    "            asset = max(asset, 0.00 - 3*0.03)\n",
    "            asset = min(asset, 0.00 + 3*0.03)\n",
    "            data[obs_ind, asset_ind] = beta[asset_ind] * z + asset\n",
    "        end\n",
    "    end\n",
    "    return 100*data\n",
    "end\n",
    "\n",
    "# We'll also write a function to simulate the\n",
    "# performance of a portfolio, and display the results\n",
    "# graphically using Gadfly\n",
    "function eval_portfolio(returns, x)\n",
    "    port_returns = returns * x\n",
    "    μ = round(mean(port_returns),2)\n",
    "    σ = round(std(port_returns),2)\n",
    "    \n",
    "    hstack(\n",
    "    # Return density plot\n",
    "    plot(x=port_returns,\n",
    "    Geom.density,\n",
    "    Scale.x_continuous(minvalue=-10,maxvalue=15),\n",
    "    Scale.y_continuous(minvalue=0.00,maxvalue=0.25),\n",
    "    xintercept=[mean(port_returns)],Geom.vline,\n",
    "    layer(x=[-10.0],y=[0.20,0.15],label=[\"μ=$μ\",\"σ=$σ\"],\n",
    "        Geom.label(position=:right))\n",
    "    ),\n",
    "    # Portfolio bar plot\n",
    "    plot(x=1:length(x), y=x,\n",
    "    Geom.bar,\n",
    "    Scale.y_continuous(minvalue=0.00,maxvalue=1.00))\n",
    "    )\n",
    "end\n",
    "\n",
    "# Lets generate some data that we'll use throughout\n",
    "srand(2000)\n",
    "# 5 assets, 10000 observations\n",
    "returns = generate_data(5, 10000);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Review of JuMP: Deterministic Portfolio Optimization\n",
    "\n",
    "To get warmed up, we'll model the classic Markowitz portfolio optmization problem. Given $n$ assets, with average returns $\\mathbf{r}$ and covariance matrix of returns $\\mathbf{\\Sigma}$, we can express the problem of finding the minimum risk portfolio for a given return as\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "\\min_{\\mathbf{x}\\geq 0} \\quad & \\mathbf{x}^T \\mathbf{\\Sigma} \\mathbf{x} \\\\\n",
    "\\text{subject to}       \\quad & \\mathbf{e}^T \\cdot \\mathbf{x} = 1 \\\\\n",
    "                              & \\mathbf{r}^T \\cdot \\mathbf{x} \\geq R\n",
    "\\end{alignat}\n",
    "$$\n",
    "You can imagine a flipped problem where we maximize return subject to a risk tolerance. We also note that we've defined risk to be the variance of the resulting portfolio - this isn't probably a good idea in practice, but this is an older-style model."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "function solve_det_port(returns, min_return)\n",
    "    n = size(returns,2)\n",
    "    # Create a JuMP Model\n",
    "    # If you aren't using Gurobi, you'll need to change this\n",
    "    detport = Model(solver=GurobiSolver(OutputFlag=0))\n",
    "    # The allocation\n",
    "    @defVar(detport, 0 <= x[1:n] <= 1)\n",
    "    # Must be a valid allocation\n",
    "    @addConstraint(detport, sum{x[i], i=1:n} == 1)\n",
    "    # Must make min return\n",
    "    r = mean(returns,1)\n",
    "    @addConstraint(detport, sum{r[i]*x[i], i=1:n} >= min_return)\n",
    "    # Objective: minimize risk (variance)\n",
    "    Σ = cov(returns)\n",
    "    @setObjective(detport, Min, sum{Σ[i,j]*x[i]*x[j], i=1:n, j=1:n})\n",
    "    # Solve it!\n",
    "    solve(detport)\n",
    "    # Return the portfolio\n",
    "    getValue(x)[:]\n",
    "end\n",
    "\n",
    "# We'll let the user modify two parameters:\n",
    "#   Unoptimized portfolio: take a convex combination of an\n",
    "#   equal-share portfolio and a maximal risk portfolio.\n",
    "#   Minimum return of optimized portfolio\n",
    "# Fun thing to try: adjust sliders until mean return\n",
    "# is the same, then compare standard deviation and the\n",
    "# allocations.\n",
    "@manipulate for λ in 0.0:0.1:1.0, minR in 0.0:0.1:2.4\n",
    "    # Construct mixed portfolio\n",
    "    mix_port = λ*0.2*ones(5) + (1-λ)*[0,0,0,0,1]\n",
    "    # Optimized portfolio\n",
    "    opt_port = solve_det_port(returns, minR)\n",
    "    # Plot\n",
    "    vstack(\n",
    "    eval_portfolio(returns, mix_port),\n",
    "    eval_portfolio(returns, opt_port)\n",
    "    )\n",
    "end\n",
    "# This will take a while the first time. This time is\n",
    "# the compilation for Gadfly - precompilation is really\n",
    "# needed, and is coming soon(TM)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## JuMPeR: Robust Portfolio Optimization\n",
    "\n",
    "In a robust portfolio optmization problem, we still have our $n$ assets and our goal is to balance risk and return. Instead of using the mean and variance of the portfolio, we'll instead try to maximize the worst-case return over our uncertainty set, i.e.\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "\\min_{\\mathbf{x}\\geq 0} \\quad & \\min_{r \\in U} \\mathbf{r}^T \\cdot \\mathbf{x} \\\\\n",
    "\\text{subject to}       \\quad & \\mathbf{e}^T \\cdot \\mathbf{x} = 1\n",
    "\\end{alignat}\n",
    "$$\n",
    "The tradeoff between risk and return is baked into the uncertainty set $U$.\n",
    "\n",
    "Polyhedral uncertainty sets are pretty popular in the literature because they don't turn (MI)LPs into (MI)SOCPs, even though an ellipsoidal set would seem to be more appropriate. The set we will do is inspired by a definition of a multivariate normal distribution. Given average returns $\\bar{\\mathbf{r}}$, a covariance matrix of returns $\\Sigma$, we can say (assuming returns are normal)\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "\\mathbf{r} & = \\Sigma^\\frac{1}{2} \\mathbf{z} + \\bar{\\mathbf{r}}\n",
    "\\end{alignat}\n",
    "$$\n",
    "We'll use that to inspire the uncertainty set:\n",
    "$$\n",
    "\\begin{alignat}{2}\n",
    "U & = \\left\\{ \\mathbf{r} = \\Sigma^{0.5} \\mathbf{z} + \\bar{\\mathbf{r}}, \\quad \\| \\mathbf{z} \\|_1 \\leq \\Gamma \\right\\}\n",
    "\\end{alignat}\n",
    "$$\n",
    "where $\\Gamma$ controls the conservativeness."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "function solve_poly_port(returns, Γ)\n",
    "    n = size(returns,2)\n",
    "\n",
    "    polyport = RobustModel(solver=GurobiSolver(OutputFlag=0))\n",
    "    @defVar(polyport, 0 <= x[1:n] <= 1)\n",
    "    @addConstraint(polyport, sum{x[i], i=1:n} == 1)\n",
    "\n",
    "    # Uncertainty\n",
    "    # Calculate the data we need\n",
    "    avg_r = mean(returns,1)\n",
    "    Σ     = cov(returns)\n",
    "    Σhalf = chol(Σ)\n",
    "    \n",
    "    # Create our Uncertains\n",
    "    # The returns\n",
    "    @defUnc(polyport, r[i=1:n])\n",
    "    # The factors\n",
    "    @defUnc(polyport, z[i=1:n])\n",
    "    # The absolute value of the factors\n",
    "    @defUnc(polyport, zabs[i=1:n] >= 0)\n",
    "    # Link all these together\n",
    "    # Constraints that are JUST on Uncertains are added\n",
    "    # to the uncertainty set automatically, not to the problem\n",
    "    for i in 1:n\n",
    "        @addConstraint(polyport, r[i] == sum{Σhalf[i,j]*z[j], j=1:n} + avg_r[i])\n",
    "        @addConstraint(polyport, zabs[i] >=  z[i])\n",
    "        @addConstraint(polyport, zabs[i] >= -z[i])\n",
    "    end\n",
    "    @addConstraint(polyport, sum{zabs[i], i=1:n} <= Γ)\n",
    "    \n",
    "    # Objective - same as before\n",
    "    @defVar(polyport, obj <= 10)\n",
    "    @setObjective(polyport, Max, obj)\n",
    "    @addConstraint(polyport, obj <= sum{r[i]*x[i], i=1:n})\n",
    "    \n",
    "    # Print it to have a look\n",
    "    #print(polyport)\n",
    "    \n",
    "    # Solve it!\n",
    "    solve(polyport)\n",
    "    #solve(polyport, prefer_cuts=true)\n",
    "    #solve(boxport, debug_printfinal=true)\n",
    "    \n",
    "    # Return the portfolio\n",
    "    getValue(x)[:]\n",
    "end\n",
    "\n",
    "\n",
    "# We'll let the user modify two parameters:\n",
    "#   Unoptimized portfolio: take a convex combination of an\n",
    "#     equal-share portfolio and a maximal risk portfolio.\n",
    "#   Γ in the robust model\n",
    "# Fun thing to try: adjust sliders until mean return\n",
    "# is the same, then compare standard deviation and the\n",
    "# allocations.\n",
    "# e.g. λ=0.3, Γ=0.5\n",
    "@manipulate for λ in 0.0:0.1:1.0, Γ in 0:0.25:5\n",
    "    # Construct mixed portfolio\n",
    "    mix_port = λ*0.2*ones(5) + (1-λ)*[0,0,0,0,1]\n",
    "    # Optimized portfolio\n",
    "    opt_port = solve_poly_port(returns, Γ)\n",
    "    # Plot\n",
    "    vstack(\n",
    "    eval_portfolio(returns, mix_port),\n",
    "    eval_portfolio(returns, opt_port)\n",
    "    )\n",
    "end\n",
    "# Will take some time the first time - thats JuMPeR\n",
    "# compiling, again just a one of cost.\n",
    "# Polyhedral uncertainty sets aren't very good for\n",
    "# this use case, it seems!"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.3.6",
   "language": "julia",
   "name": "julia 0.3"
  },
  "language_info": {
   "name": "julia",
   "version": "0.3.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
